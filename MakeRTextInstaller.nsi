; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "RText"
!define PRODUCT_VERSION "2.5.7"
!define PRODUCT_WEB_SITE "http://fifesoft.com/rtext/"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\RText.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "src\main\dist\License.txt"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\RText.exe"
!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\Readme.txt"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "rtext_${PRODUCT_VERSION}_win32_setup.exe"
InstallDir "$PROGRAMFILES\RText"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section "MainSection" SEC01
  WriteRegStr HKLM "Software\RText" "Location" "$INSTDIR"
  SetOutPath "$INSTDIR"
  SetOverwrite on
  File "RText.exe"
  CreateDirectory "$SMPROGRAMS\RText"
  CreateShortCut "$SMPROGRAMS\RText\RText.lnk" "$INSTDIR\RText.exe"
  CreateShortCut "$DESKTOP\RText.lnk" "$INSTDIR\RText.exe"
  SetOverwrite on
  SetOutPath "$INSTDIR"
  File /r "build\install\rtext\*"
SectionEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\RText\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\RText\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\RText.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\RText.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  ; Don't just Delete /r $INSTDIR, preserve files they added themselves
  SetOutPath "$INSTDIR"
  Delete "$INSTDIR\RText.exe"
  Delete "$INSTDIR\RText.url"
  Delete "$INSTDIR\uninst.exe"
  Delete /REBOOTOK "$INSTDIR\autocomplete*"
  Delete /REBOOTOK "$INSTDIR\AutoComplete.License.txt"
  Delete /REBOOTOK "$INSTDIR\english_dic.zip"
  Delete /REBOOTOK "$INSTDIR\ExtraFileChooserFilters.xml"
  Delete /REBOOTOK "$INSTDIR\fife.common*"
  Delete /REBOOTOK "$INSTDIR\Jazzy.LICENSE.txt"
  Delete /REBOOTOK "$INSTDIR\js-14*.jar" ; Shouldn't be there, but was put by older releases
  Delete /REBOOTOK "$INSTDIR\License.txt"
  Delete /REBOOTOK "$INSTDIR\localizations.xml"
  Delete /REBOOTOK "$INSTDIR\README.jazzy.txt"
  Delete /REBOOTOK "$INSTDIR\README.spellchecker.txt"
  Delete /REBOOTOK "$INSTDIR\Readme.txt"
  Delete /REBOOTOK "$INSTDIR\readme.unix"
  Delete /REBOOTOK "$INSTDIR\Rhino.build-date" ; Shouldn't be there, but was put by older releases
  Delete /REBOOTOK "$INSTDIR\Rhino.LICENSE.txt" ; Shouldn't be there, but was put by older releases
  Delete /REBOOTOK "$INSTDIR\RSTALanguageSupport.License.txt" ; Shouldn't be there, but was put by older releases
  Delete /REBOOTOK "$INSTDIR\RSTAUI.*.txt" ; License and readme
  Delete /REBOOTOK "$INSTDIR\rstaui*"
  Delete /REBOOTOK "$INSTDIR\rsta_spellchecker.jar"
  Delete /REBOOTOK "$INSTDIR\spellchecker*"
  Delete /REBOOTOK "$INSTDIR\rsyntaxtextarea*"
  Delete /REBOOTOK "$INSTDIR\RSyntaxTextArea.License.txt"
  Delete /REBOOTOK "$INSTDIR\RText.jar"
  Delete /REBOOTOK "$INSTDIR\SpellChecker.License.txt"
  Delete /REBOOTOK "$INSTDIR\Win32FileIOExtras.dll"
  Delete /REBOOTOK "$INSTDIR\x64FileIOExtras.dll"
  Delete /REBOOTOK "$INSTDIR\doc\HelpDialogContents.xml"
  Delete /REBOOTOK "$INSTDIR\doc\en\*"
  Delete /REBOOTOK "$INSTDIR\exampleMacros\*"
  Delete /REBOOTOK "$INSTDIR\icongroups\BlueSphereIcons.jar"
  Delete /REBOOTOK "$INSTDIR\icongroups\EclipseIcons.jar"
  Delete /REBOOTOK "$INSTDIR\icongroups\ExtraIcons.xml"
  Delete /REBOOTOK "$INSTDIR\icongroups\JavaIcons.jar"
  Delete /REBOOTOK "$INSTDIR\icongroups\XPIcons.jar"
  Delete /REBOOTOK "$INSTDIR\lnfs\lookandfeels.xml"
  Delete /REBOOTOK "$INSTDIR\lnfs\OfficeLnFs.jar"
  Delete /REBOOTOK "$INSTDIR\lnfs\OfficeLnFs.License.txt"
  Delete /REBOOTOK "$INSTDIR\lnfs\substance\*jar"
  Delete /REBOOTOK "$INSTDIR\lnfs\substance.jar" ; Possibly there from old releases
  Delete /REBOOTOK "$INSTDIR\lnfs\trident.jar" ; Possibly there from old releases
  Delete /REBOOTOK "$INSTDIR\plugins\groovy-all-*.jar"
  Delete /REBOOTOK "$INSTDIR\plugins\groovy.LICENSE.txt"
  Delete /REBOOTOK "$INSTDIR\plugins\js-14*.jar" ; Old and new versions
  Delete /REBOOTOK "$INSTDIR\plugins\jsonbeans-*.jar"
  Delete /REBOOTOK "$INSTDIR\plugins\jsonbeans.LICENSE.txt"
  Delete /REBOOTOK "$INSTDIR\plugins\jtidy-r938.jar"
  Delete /REBOOTOK "$INSTDIR\plugins\jtidy-r938.LICENSE.txt"
  Delete /REBOOTOK "$INSTDIR\plugins\languagesupport-*.jar"
  Delete /REBOOTOK "$INSTDIR\plugins\plugin-console.jar"
  Delete /REBOOTOK "$INSTDIR\plugins\plugin-fileSystemTree.jar"
  Delete /REBOOTOK "$INSTDIR\plugins\plugin-heapIndicator.jar"
  Delete /REBOOTOK "$INSTDIR\plugins\plugin-languageSupport.jar"
  Delete /REBOOTOK "$INSTDIR\plugins\plugin-macros.jar"
  Delete /REBOOTOK "$INSTDIR\plugins\plugin-sourceBrowser.jar"
  Delete /REBOOTOK "$INSTDIR\plugins\plugin-tasks.jar"
  Delete /REBOOTOK "$INSTDIR\plugins\plugin-projects.jar"
  Delete /REBOOTOK "$INSTDIR\plugins\plugin-tools.jar"
  Delete /REBOOTOK "$INSTDIR\plugins\plugin-tidy.jar"
  Delete /REBOOTOK "$INSTDIR\plugins\rhino-*.jar"
  Delete /REBOOTOK "$INSTDIR\plugins\Rhino.build-date"
  Delete /REBOOTOK "$INSTDIR\plugins\Rhino.LICENSE.txt"

  Delete /REBOOTOK "$INSTDIR\plugins\Console.jar" ; Older name
  Delete /REBOOTOK "$INSTDIR\plugins\FileSystemTree.jar" ; Older name
  Delete /REBOOTOK "$INSTDIR\plugins\HeapIndicator.jar" ; Older name
  Delete /REBOOTOK "$INSTDIR\plugins\language_support.jar" ; Older name
  Delete /REBOOTOK "$INSTDIR\plugins\MacroSupport.jar" ; Older name
  Delete /REBOOTOK "$INSTDIR\plugins\ProjectSupport.jar" ; Older name
  Delete /REBOOTOK "$INSTDIR\plugins\RSTALanguageSupport.License.txt" ; Older name
  Delete /REBOOTOK "$INSTDIR\plugins\RTextLanguageSupport.jar" ; Older name
  Delete /REBOOTOK "$INSTDIR\plugins\SourceBrowser.jar" ; Older name
  Delete /REBOOTOK "$INSTDIR\plugins\TaskList.jar" ; Older name
  Delete /REBOOTOK "$INSTDIR\plugins\tidy.jar" ; Older name
  Delete /REBOOTOK "$INSTDIR\plugins\ToolSupport.jar" ; Older name
  Delete /REBOOTOK "$INSTDIR\plugins\language_support.jar" ; Older name

  Delete "$SMPROGRAMS\RText\Uninstall.lnk"
  Delete "$SMPROGRAMS\RText\Website.lnk"
  Delete "$DESKTOP\RText.lnk"
  Delete "$SMPROGRAMS\RText\RText.lnk"

  ; Folders will be removed if they are completely empty (user didn't add anything)
  RMDir "$SMPROGRAMS\RText"
  RMDir "$INSTDIR\doc\en"
  RMDir "$INSTDIR\doc"
  RMDir "$INSTDIR\exampleMacros"
  RMDir "$INSTDIR\icongroups"
  RMDir "$INSTDIR\lnfs\substance"
  RMDir "$INSTDIR\lnfs"
  RMDir "$INSTDIR\plugins"
  SetOutPath "$TEMP" ; So we can remove the root dir if it's empty
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd